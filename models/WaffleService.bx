import ortus.boxlang.runtime.BoxRuntime;

class {

	/**
	 * Generate the waffle URL
	 */
	private string function getWaffleURL() {
		config = BoxRuntime.getInstance().getModuleService().getModuleSettings( 'bx-waffle' );
		tags = config.imageTags.toList();
		return 'https://www.flickr.com/services/feeds/photos_public.gne?tags=#tags#&tagmode=all&format=json&nojsoncallback=1';
	}

	/**
	 * Get a declicious waffle image data
	 */
	public Struct function getWaffleImage() {
		bx:http url=getWaffleURL() timeout=10 result="result";
		if( !isJSON( result.fileContent ) ) {
			println( result );
			throw( 'Invalid JSON returned from Flickr' );
		}
		data = JSONDeserialize( result.fileContent );
		return data.items[ randRange(1, data.items.len() ) ];
	}

	/**
	 * Get waffle embed code
	 */
	public string function getWaffleEmbed() {
		image = getWaffleImage();
		return '<img src="#image.media.m#" alt="#image.title#" />';
	}

	/**
	 * Write the waffle embed code to buffer
	 */
	@output( true )
	public void function writeWaffleEmbed() {
		echo( getWaffleEmbed() );
	}

}